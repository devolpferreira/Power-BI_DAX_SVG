PRODUTO_PRINCIO_ATIVO_STATUS_QUANTIADE_SVG= 

VAR vWidth  = 430
VAR vHeight = 70

VAR vFontGlobal = [_Fonte_Padrao]

VAR vProduto =
    RELATED ( dBaseProdutos[DESCRIÇÃO PRODUTO] )

VAR vNomeTecnico =
    fEstoqueGeral[NOME_TECNICO_VALIDO]

VAR vEstoqueTexto =
    fEstoqueGeral[Estoque Unidade]

VAR vStatusValidade =
    UPPER ( TRIM ( fEstoqueGeral[STATUS AJUSTADO] ) )

VAR vStatusEstoque =
    UPPER ( TRIM ( fEstoqueGeral[STATUS ESTOQUE] ) )

VAR vSemEstoque =
    vStatusEstoque = "SEM ESTOQUE"

-- =========================================
-- COR STATUS (BORDA + BARRA ESQUERDA)
-- =========================================

VAR vCorStatus =
    SWITCH (
        vStatusValidade,
        "VENCIDO", "#E74C3C",
        "VENCENDO", "#F1C40F",
        "A VENCER", "#16A085",
        "SEM DT VENCIMENTO", "#A8A4A4",
        "#A8A4A4"
    )

VAR vCorBarraEsquerda = vCorStatus

VAR vCorBorda =
    IF ( vSemEstoque, "#FFFFFF", vCorStatus )

-- =========================================
-- BARRA DIREITA (STATUS ESTOQUE)
-- =========================================

VAR vCorBarraDireita =
    IF ( vSemEstoque, "#BFBFBF", "#16A085" )

VAR vTextoVertical =
    IF ( vSemEstoque, "Sem Estoque", "Com Estoque" )

VAR vCorTextoVertical =
    IF ( vSemEstoque, "#060606", "white" )

-- =========================================
-- TEXTO ESTOQUE
-- =========================================

VAR vCorTextoEstoque =
    IF ( vSemEstoque, "#E74C3C", "black" )

-- =========================================
-- TEXTO DO BADGE (REMOVE "-")
-- =========================================

VAR vTextoBadge =
    IF (
        vSemEstoque
            || vStatusValidade = "-"
            || vStatusValidade = ""
            || ISBLANK ( vStatusValidade ),
        "",
        vStatusValidade
    )

VAR vCorBadgeBg = "#FFFFFF"

VAR vCorBadgeStroke =
    IF ( vTextoBadge = "", "none", vCorStatus )

VAR vCorTextoBadge =
    IF ( vTextoBadge = "", "none", vCorStatus )

RETURN
"data:image/svg+xml;utf8,
<svg viewBox='0 0 402 68'
     xmlns='http://www.w3.org/2000/svg'>

  <!-- FUNDO -->
  <rect width='402' height='68' rx='6'
        fill='#FFFFFF'
        stroke='" & vCorBorda & "'
        stroke-width='0.8'/>

  <!-- BARRA ESQUERDA -->
  <rect width='8' height='68'
        fill='" & vCorBarraEsquerda & "'/>

  <!-- BARRA DIREITA -->
  <rect x='386' width='16' height='68'
        fill='" & vCorBarraDireita & "'/>

  <!-- TEXTO VERTICAL -->
  <text x='394'
        y='36'
        transform='rotate(90 394 34)'
        text-anchor='middle'
        font-size='9'
        font-family='" & vFontGlobal & "'
        font-weight='700'
        fill='" & vCorTextoVertical & "'>
        " & vTextoVertical & "
  </text>

  <!-- PRODUTO -->
  <text x='25' y='22'
        font-size='12'
        font-family='" & vFontGlobal & "'
        fill='#9B9B9B'>Produto</text>

  <text x='25' y='44'
        font-family='" & vFontGlobal & "'
        font-size='11'
        font-weight='600'>
        " & vProduto & "
  </text>

  <text x='25' y='59'
        font-family='" & vFontGlobal & "'
        font-size='9'
        font-style='italic'
        fill='#7D7D7D'>
        " & vNomeTecnico & "
  </text>

  <!-- ESTOQUE -->
  <text x='290' y='22'
        font-family='" & vFontGlobal & "'
        font-size='12'
        fill='#9B9B9B'>Estoque</text>

  <text x='290' y='44'
        font-family='" & vFontGlobal & "'
        font-size='13'
        font-weight='600'
        fill='" & vCorTextoEstoque & "'>
        " & vEstoqueTexto & "
  </text>

  <!-- BADGE -->
  <rect x='158' y='8'
        width='115'
        height='20'
        rx='10'
        fill='" & vCorBadgeBg & "'
        stroke='" & vCorBadgeStroke & "'
        stroke-width='0.8'/>

  <text x='215'
        y='22'
        font-family='" & vFontGlobal & "'
        text-anchor='middle'
        font-size='10'
        font-weight='600'
        fill='" & vCorTextoBadge & "'>
        " & vTextoBadge & "
  </text>

</svg>"
